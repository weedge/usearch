import org.gradle.internal.jvm.Jvm

plugins {
    id 'java-library'
    id 'cpp'
    id 'maven-publish'
}

group "cloud.unum"
version = file("VERSION").text.trim()

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir "java/"
            exclude "test/"
        }
    }
    test {
        java {
            srcDir "java/test"
        }
    }
}

dependencies {
    testImplementation('junit:junit:4.13.2')
}

model {

    components {
        usearch(NativeLibrarySpec) {
            sources {
                cpp {
                    source {
                        srcDirs "java/cloud/unum/usearch"
                        include "**/*.cpp"
                    }
                    exportedHeaders {
                        srcDirs "include", "fp16/include", "simsimd/include", "${Jvm.current().javaHome}/include"
                    }
                }
            }
            binaries.withType(StaticLibraryBinarySpec) {
                buildable = false
            }
            binaries.withType(SharedLibraryBinarySpec) {
                if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
                    cppCompiler.args '-D_FILE_OFFSET_BITS=64'
                    cppCompiler.args '-std=c++11'
                } else if (targetPlatform.operatingSystem.macOsX) {
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include/darwin"
                    cppCompiler.args '-D_FILE_OFFSET_BITS=64'
                    cppCompiler.args '-std=c++11'
                } else if (targetPlatform.operatingSystem.windows){
                    cppCompiler.args "-I${Jvm.current().javaHome}/include/win32"
                    cppCompiler.args '/std:c++11'
                }
            }
        }
    }
}

test {
    forkEvery 1

    dependsOn jar

    // Rearrange test classpath, add compiled JAR instead of main classes
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(jar.archiveFile)
}

jar {
    dependsOn("usearchSharedLibrary")
    into("usearch") {
        from("$buildDir/libs/usearch/shared/libusearch.dylib")
        from("$buildDir/libs/usearch/shared/libusearch.so")
    }
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java

            pom {
                name = "USearch"
                description = "Smaller & Faster Single-File Vector Search Engine from Unum"
                url = "https://github.com/unum-cloud/usearch"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/unum-cloud/usearch.git'
                    developerConnection = 'scm:git:ssh://github.com/unum-cloud/usearch.git'
                    url = 'https://github.com/unum-cloud/usearch'
                }

            }
        }
    }


    // Publishing to GitHub is much easier ;)
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/unum-cloud/usearch"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}